#!/usr/bin/env perl
###
### Munin plugin for CouchDB HTTP request monitoring
###

# Parameters:
#
# config
# autoconf

#%# family=auto
#%# capabilities=autoconf

=head1 NAME

couchdb_httpd_requests - Munin plugin to monitor the number of requests to
CouchDB servers.

=head1 APPLICABLE SYSTEMS

CouchDB servers with statistics enabled.

=head1 CONFIGURATION

=head2 Environment variables

=over

=item *

B<uri>: Set the URI for access to the _stats endpoint for the target CouchDB
instance. This defaults to B<http://localhost:5984/_stats>.

=back

=head1 INTERPRETATION

The plugin shows the total number of requests to the CouchDB server.

=head1 BUGS

Yep.

=head1 AUTHOR

Gordon Stratton <gordon.stratton@gmail.com>

=head1 LICENSE

Beerware

=cut

my $ret = undef;

if (!eval("require LWP::UserAgent;")) {
	$ret = "LWP::UserAgent not found";
}

if (!eval("require JSON;")) {
	$ret = "JSON not found";
}

my $URI = exists($ENV{'uri'}) ? $ENV{'uri'} : "http://localhost:5984/_stats";

if (defined($ARGV[0]) && $ARGV[0] eq "autoconf")
{
	if ($ret)
	{
		print "no ($ret)\n";
		exit 1;
	}

	my $response = LWP::UserAgent->new(timeout => 5)->request(HTTP::Request->new('GET', $URI));

	if ($response->is_success) {
		print "yes\n";
		exit 0;
	} else {
		print "no\n";
		exit 1;
	}
}

my @REQUEST_TYPES = ('requests', 'bulk_requests', 'view_reads',
  'clients_requesting_changes', 'temporary_view_reads');

my $response = LWP::UserAgent->new(timeout => 5)->request(HTTP::Request->new('GET', $URI));

if (defined($ARGV[0]) && $ARGV[0] eq "config") {
	print "graph_title CouchDB httpd requests\n";
	print "graph_args --base 1000\n";
	print "graph_vlabel requests / \${graph_period}\n";
	print "graph_category couchdb\n";
	foreach (@REQUEST_TYPES) {
		if ($response->is_success) {
			my $json = JSON::decode_json($response->content);
			print "$_.label $json->{'httpd'}->{$_}->{'description'}\n";
		}
		print "$_.type COUNTER\n";
		print "$_.min 0\n";
		print "$_.max 1000000\n";
	}
	exit 0;
}

if ($response->is_success) {
	my $json = JSON::decode_json($response->content);
	foreach (@REQUEST_TYPES) {
		if (defined($json->{'httpd'}->{$_})) {
			print "$_.value $json->{'httpd'}->{$_}->{'current'}\n";
		} else {
			print "$_.value U\n";
		}
	}
} else {
	foreach (@REQUEST_TYPES) {
		print "$_.value U\n";
	}
}
